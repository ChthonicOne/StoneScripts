var g = new gear

var counter = 0
var reset = 0   // This will go away when I get a strong splash Life sword
var end = 0
?loc = crypt
  ?loc.loop
    counter = 0
    reset = 0
    end = 0
  reset = 0
  ?foe = boss & hp < 11
    activate potion
  ?foe = Booo & end = 0
    Ghost()
  ?foe ! Booo & foe ! boss & end = 0
    MeleeFoe()
  :?foe = boss | end = 1
    ?foe.state = 32
      counter++
    :
      counter = 0
      reset = 1
    ?foe = skinless
      PallasSkinless()
    :
      end = 1
      PallasLegless()
            
func MeleeFoe()
  ?foe.distance > 22
    g.Trisk(l)
    g.LifeShield()
  :?foe.distance <= 22 & foe.distance > 16 
    g.LifeCritbow()
  :?foe.distance <= 16 & foe.distance >= 10
    ?item.GetCooldown(＂dash＂) <= 0
      g.Dash()
    :
      g.Bash()
  :?foe.distance <= 9
    ?hp < maxhp
      g.LifeStealSwordAndShield()
    :
      g.LifeSword(l)
      g.LifeBigSword(r)
    
func Ghost()
  ?foe.distance >= 21
    g.Trisk(l)
    g.LifeShield()
  :
    ?foe.debuffs.count = 0
      g.IceSlowWand(l)
    :
      g.LifeStealWand(l)
    g.LifeWand(r)

func PallasSkinless()
  >`0,11,@counter@
  ?counter = 65 & item.GetCooldown(＂mind＂) = 0 & armor < 1 // I'd rather get hit while I have armor
    g.Mind(r)
  :?foe.distance > 22
    g.Trisk(l)
    g.LifeShield()
  :
    g.LifeCritbow()

func PallasLegless() // He has a ghost, but the ghost never gets close as we're close to him so we shouldn't have to worry.
  ?foe.distance > 16
    g.Trisk(l)
    g.LifeShield()
  :?foe.distance <= 16 & foe.distance >= 10
    ?item.GetCooldown(＂bash＂) <= 0 & foe.count > 1
      g.Bash()
    :
      g.Dash()
  :
    ?foe.count > 1
      g.LifeBigSword(l)
    :
      g.LifeSword(l)
    ?foe.debuffs.string ! ＂dot:＂
      g.FireSword(r)
    :?foe.debuffs.string ! "damage:"
      g.PoisonSword(r)
    :
      ?hp < maxhp - 30 | reset = 1
        ?foe.count > 1
          g.LifeStealBigSword(r)
        :
          g.LifeStealSword(r)
      :
        ?foe.count > 1
          g.IceSword(r)
        :
          g.LifeBigSword(r)